#version 400 core

in vec2 pass_textureCoords;
in vec3 surfaceNormal;
in vec3 toLightVector[6];
in vec3 toCameraVector;
in float visibility;

out vec4 out_Color;

uniform sampler2D textureSampler;
uniform sampler2D rSampler;
uniform sampler2D gSampler;
uniform sampler2D bSampler;
uniform sampler2D blendMap;



uniform vec3 lightColor[6];
uniform vec3 attenuation[6];
uniform float shineDamper;
uniform float reflectivity;
uniform vec3 skyColor;

void main(void) {

	vec4 blendMapColor = texture(blendMap, pass_textureCoords);
	
	float backTextureAmount = 1 - (blendMapColor.r + blendMapColor.g + blendMapColor.b);
	vec2 tileCoords = pass_textureCoords * 160.0;
	vec4 backgroundTextureColor = texture(textureSampler, tileCoords) * backTextureAmount;
	vec4 rTextureColor = texture(rSampler, tileCoords) * blendMapColor.r;
	vec4 gTextureColor = texture(gSampler, tileCoords) * blendMapColor.g;
	vec4 bTextureColor = texture(bSampler, tileCoords) * blendMapColor.b;
	
	vec4 totalColor = backgroundTextureColor + rTextureColor + gTextureColor + bTextureColor;
	vec3 unitNormal = normalize(surfaceNormal);
	vec3 unitVectorToCamera = normalize(toCameraVector);
	
	vec3 totalDiffuse = vec3(0.0);
	vec3 totalSpecular = vec3(0.0);
	
	for(int i = 0; i < 6; i++) {
		float distance = length(toLightVector[i]);
		float attenuationFactor = attenuation[i].x + (attenuation[i].y * distance) + (attenuation[i].z * distance * distance);
		vec3 unitLightVector = normalize(toLightVector[i]);
		float normalDot = dot(unitNormal, unitLightVector);
		float brightness = max(normalDot, 0.0);
		vec3 lightDirection = -unitLightVector;
		vec3 reflectedLight = reflect(lightDirection, unitNormal);
		float specularFactor = dot(reflectedLight, unitVectorToCamera);
		specularFactor = max(specularFactor, 0.0);
		float dampedFactor = pow(specularFactor, shineDamper);
		totalDiffuse = totalDiffuse + (brightness * lightColor[i])/attenuationFactor;
		totalSpecular = totalSpecular + (dampedFactor * reflectivity * lightColor[i])/attenuationFactor;
	}
	
	totalDiffuse = max(totalDiffuse, 0.4);
	
	out_Color = vec4(totalDiffuse, 1.0) * totalColor + vec4(totalSpecular, 1.0);
	out_Color = mix(vec4(skyColor, 1.0), out_Color, visibility);
}